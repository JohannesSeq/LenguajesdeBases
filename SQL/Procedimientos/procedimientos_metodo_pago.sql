
CREATE OR REPLACE PACKAGE PKG_METODO_PAGO AS
    PROCEDURE AGREGAR_METODO(
        P_NOMBRE VARCHAR,
        P_DESCRIPCION VARCHAR,
        P_COMENTARIO VARCHAR
    );

    PROCEDURE MODIFICAR_METODO(
        P_ID_METODO NUMBER,
        P_NUEVO_NOMBRE VARCHAR,
        P_NUEVA_DESCRIPCION VARCHAR
    );

    PROCEDURE ELIMINAR_METODO(
        P_ID_METODO NUMBER,
        P_COMENTARIO VARCHAR
    );

    PROCEDURE ENVIO_TOTAL_METODOS(P_CURSOR OUT SYS_REFCURSOR);
    PROCEDURE ENVIO_METODO_INDIVIDUAL(P_ID_METODO NUMBER, P_CURSOR OUT SYS_REFCURSOR);
END PKG_METODO_PAGO;
/


CREATE OR REPLACE PACKAGE BODY PKG_METODO_PAGO AS

    PROCEDURE AGREGAR_METODO(
        P_NOMBRE VARCHAR,
        P_DESCRIPCION VARCHAR,
        P_COMENTARIO VARCHAR
    ) AS
        V_ID NUMBER;
        V_ESTADO_ID VARCHAR(250);
    BEGIN
        INSERT INTO METODO_PAGO(NOMBRE_METODO, DESCRIPCION)
        VALUES (P_NOMBRE, P_DESCRIPCION)
        RETURNING ID_METODO INTO V_ID;

        V_ESTADO_ID := CREAR_ENTRADA_ESTADO(TO_CHAR(V_ID), 'METODO_PAGO', P_COMENTARIO);

        UPDATE METODO_PAGO
        SET ID_ESTADO = V_ESTADO_ID
        WHERE ID_METODO = V_ID;

        COMMIT;
    END;

    PROCEDURE MODIFICAR_METODO(
        P_ID_METODO NUMBER,
        P_NUEVO_NOMBRE VARCHAR,
        P_NUEVA_DESCRIPCION VARCHAR
    ) AS
    BEGIN
        UPDATE METODO_PAGO
        SET NOMBRE_METODO = P_NUEVO_NOMBRE,
            DESCRIPCION = P_NUEVA_DESCRIPCION
        WHERE ID_METODO = P_ID_METODO;

        COMMIT;
    END;

    PROCEDURE ELIMINAR_METODO(
        P_ID_METODO NUMBER,
        P_COMENTARIO VARCHAR
    ) AS
        V_ESTADO_ID VARCHAR(250);
    BEGIN
        V_ESTADO_ID := CREAR_ENTRADA_ESTADO(TO_CHAR(P_ID_METODO), 'METODO_PAGO', P_COMENTARIO);

        UPDATE METODO_PAGO
        SET ID_ESTADO = V_ESTADO_ID
        WHERE ID_METODO = P_ID_METODO;

        COMMIT;
    END;

    PROCEDURE ENVIO_TOTAL_METODOS(P_CURSOR OUT SYS_REFCURSOR) AS
    BEGIN
        OPEN P_CURSOR FOR SELECT * FROM VISTA_METODO_PAGO;
    END;

    PROCEDURE ENVIO_METODO_INDIVIDUAL(P_ID_METODO NUMBER, P_CURSOR OUT SYS_REFCURSOR) AS
    BEGIN
        OPEN P_CURSOR FOR
        SELECT * FROM VISTA_METODO_PAGO WHERE ID_METODO = P_ID_METODO;
    END;

END PKG_METODO_PAGO;
/

CREATE OR REPLACE PROCEDURE ELIMINAR_METODO_PAGO (
    P_ID_METODO IN NUMBER,
    P_RESULTADO OUT VARCHAR
)
AS
    V_ID_ESTADO VARCHAR(250);
    V_EXISTE_FACTURA NUMBER := 0;
BEGIN
    -- Verificar si el método de pago está asociado a alguna factura
    SELECT COUNT(*) INTO V_EXISTE_FACTURA
    FROM FACTURAS
    WHERE ID_METODO = P_ID_METODO;

    IF V_EXISTE_FACTURA > 0 THEN
        P_RESULTADO := 'REFERENCIAS_ACTIVAS';
        RETURN;
    END IF;

    -- Obtener el estado asociado al método de pago
    SELECT ID_ESTADO INTO V_ID_ESTADO
    FROM METODO_PAGO
    WHERE ID_METODO = P_ID_METODO;

    -- Eliminar método de pago y su estado
    DELETE FROM METODO_PAGO WHERE ID_METODO = P_ID_METODO;
    DELETE FROM ESTADOS WHERE ID_ESTADO = V_ID_ESTADO;

    COMMIT;

    P_RESULTADO := 'BORRADO_FISICO';
END;
/
